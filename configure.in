dnl Process this file with autoconf to produce a configure script.
AC_INIT(src/accept.c)

AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(MasqMail, 0.1.17)

AC_PREFIX_DEFAULT(/usr)

dnl Checks for programs.

dnl Checks for libraries.

AC_PROG_CC
AC_ISC_POSIX
AC_STDC_HEADERS
AC_ARG_PROGRAM
AC_PROG_RANLIB

AM_PATH_GLIB

dnl checks necessary for libc5:
dnl if there is res_search in libc, it is probably libc5
dnl if not, it is probably libc6 and we need libresolv
AC_CHECK_LIB(c, res_search, need_resolv=no, need_resolv=yes)
if test "$need_resolv" = yes; then
	AC_CHECK_LIB(resolv, res_search,has_resolv=yes,AC_MSG_ERROR("no libresolv"))
	RESOLV_LIBS='-lresolv'
else
	RESOLV_LIBS=''
fi
AC_SUBST(RESOLV_LIBS)

dnl if there is no getline, we define it using getdelim in src/masqmail.h
AC_CHECK_FUNCS(getline)

dnl if there is no fdatasync, we define it to fsync in src/masqmail.h
AC_CHECK_FUNCS(fdatasync)

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h sys/time.h syslog.h unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_HEADER_TIME
AC_STRUCT_TM

dnl Checks for library functions.
AC_FUNC_FNMATCH
AC_TYPE_SIGNAL
AC_FUNC_STRFTIME
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(select socket strerror strstr)

dnl user and group configuration
AC_ARG_WITH(user,
	[  --with-user=USER         set user [mail]],
	)
if test "$with_user" = ''; then
	with_user='mail'
fi

AC_ARG_WITH(group,
	[  --with-group=GROUP       set group [trusted]],
	)
if test "$with_group" = ''; then
	with_group='trusted'
fi

AC_DEFINE_UNQUOTED(DEF_MAIL_USER, "${with_user}")
AC_SUBST(with_user)
AC_DEFINE_UNQUOTED(DEF_MAIL_GROUP, "${with_group}")
AC_SUBST(with_group)

dnl optional features
MD5_LIBS=''
BASE64_LIBS=''

dnl pop support (default is use it)
AC_ARG_ENABLE(pop3,
	[  --disable-pop3           disable pop3 support],
	if test "$enable_pop3" != 'no'; then
		pop3_enabled='yes'
	fi,
	pop3_enabled='yes'
	)
if test "$pop3_enabled" = yes; then
	AC_DEFINE(ENABLE_POP3, 1)
	MD5_LIBS='md5/libmd5c.a'
fi

dnl auth support (default is to not use it)
AC_ARG_ENABLE(auth,
	[  --enable-auth            enable AUTH (RFC 2554) client support],
	if test "$enable_auth" != 'no'; then
		auth_enabled='yes'
	fi,
	)
if test "$auth_enabled" = yes; then
	AC_DEFINE(ENABLE_AUTH, 1)
	BASE64_LIBS='base64/libbase64.a'
	MD5_LIBS='md5/libmd5c.a'
fi
AC_SUBST(MD5_LIBS)
AC_SUBST(BASE64_LIBS)

dnl ident support (default is to not use it)
IDENT_LIBS=''
AC_ARG_ENABLE(ident,
	[  --enable-ident           enable ident (RFC 1413) support],
	if test "$enable_ident" != 'no'; then
		ident_enabled='yes'
	fi,
	)
AC_SUBST(has_ident)
if test "$ident_enabled" = yes; then
	AC_DEFINE(ENABLE_IDENT, 1)
	AC_CHECK_LIB(ident, ident_id, IDENT_LIBS='-lident', IDENT_LIBS='libident/libident.a')
fi
AC_SUBST(IDENT_LIBS)

dnl liblockfile
AC_ARG_WITH(liblockfile,
	[  --with-liblockfile       use liblock (for Debian)],
	)
if test "$with_liblockfile" != ''; then
	with_liblockfile='yes'
fi
if test "$with_liblockfile" = yes; then
	AC_CHECK_LIB(lockfile, maillock, has_lockfile=yes, AC_MSG_ERROR("no liblockfile"))
	LOCKFILE_LIBS='-llockfile'
	AC_DEFINE(USE_LIBLOCKFILE, 1)
else
	LOCKFILE_LIBS=''
fi
AC_SUBST(LOCKFILE_LIBS)
AC_SUBST(USE_LIBLOCKFILE)

dnl log and spool directories
AC_ARG_WITH(logdir,
	[  --with-logdir=DIR        set log directory [/var/masqmail]],
	,
        with_logdir='/var/masqmail/'
	)
AC_SUBST(with_logdir)

AC_ARG_WITH(spooldir,
	[  --with-spooldir=DIR      set spool directory [/var/spool/masqmail]],
	,
	with_spooldir='/var/spool/masqmail/'
	)
AC_SUBST(with_spooldir)

dnl configuration file
AC_ARG_WITH(conffile,
	[  --with-conffile          filename of main configuration [/etc/masqmail.conf]],
	,
	with_conffile='/etc/masqmail.conf'
	)
AC_DEFINE_UNQUOTED(CONF_FILE, "${with_conffile}")
AC_SUBST(with_conffile)

AC_OUTPUT(Makefile \
	docs/Makefile \
	docs/man/Makefile \
	tests/Makefile \
	src/Makefile \
	src/base64/Makefile \
	src/md5/Makefile \
	src/libident/Makefile \
	suse/masqmail.spec \
	redhat/masqmail.spec
	)
